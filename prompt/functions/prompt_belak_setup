#
# Originally based on the sorin theme from prezto and the gentoo prompt
# with as much unicode removed as possible.
#
# Authors:
#   Kaleb Elwert <belak@coded.io>
#

# This is the last remaining portion from prezto. It's adapted from what became
# the prompt-pwd function.
function belak_prompt_pwd {
  setopt local_options extended_glob

  local current_pwd="${PWD/#$HOME/~}"
  local ret_directory

  if [[ "$current_pwd" == (#m)[/~] ]]; then
    ret_directory="$MATCH"
    unset MATCH
  else
    ret_directory="${${${${(@j:/:M)${(@s:/:)current_pwd}##.#?}:h}%/}//\%/%%}/${${current_pwd:t}//\%/%%}"
  fi

  print "$ret_directory"
}

function prompt_belak_venv {
  _prompt_belak_venv=""
  if [[ -n $VIRTUAL_ENV ]]; then
    _prompt_belak_venv=" [${VIRTUAL_ENV##*/}]"
  fi
}

function prompt_belak_precmd {
  _prompt_belak_pwd=$(belak_prompt_pwd)
  prompt_belak_venv
}

function prompt_belak_setup {
  # When loaded through the prompt command, these prompt_* options will be
  # enabled simply by exporting this array.
  prompt_opts=(cr percent sp subst)

  # Load required functions
  autoload -Uz add-zsh-hook

  # Add hook for updating info when we need it
  add-zsh-hook precmd prompt_belak_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%B%F{7}...%f%b'

  _prompt_belak_pwd=''
  _prompt_belak_venv=''

  # Define prompts.
  PROMPT='${SSH_TTY:+"%F{10}%n@%m%f "}%F{10}${_prompt_belak_pwd}%f${_prompt_belak_venv}%(!. %B%F{1}#%f%b.) %(?:%F{12}:%F{9})%#%f '
  RPROMPT='${editor_info[overwrite]}${VIM:+" %B%F{6}V%f%b"}'
  SPROMPT='zsh: correct %F{1}%R%f to %F{2}%r%f [nyae]? '
}

function prompt_belak_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2>/dev/null
  prompt_preview_theme 'belak'
}

prompt_belak_setup "$@"
